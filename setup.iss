; Setup wizard for Set-ClassicTheme
; Version 1.1.0
; Copyright © Ziv Kedem 2020
; Licensed under the GNU GPL version 3; see LICENSE.txt for details.
; Script mostly generated by the Inno Script Studio Wizard.

#define MyAppName "Set-ClassicTheme"
#define MyAppVersion "1.1.0"
#define MyAppPublisher "zkedem"
#define MyAppExeName "Set-ClassicTheme.ps1"
#define MyAppURL "https://github.com/zkedem/Set-ClassicTheme"

#include "environment.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E866ADE2-1B4E-4348-96AD-58FE0A6C2B37}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile={#SourcePath}\LICENSE.txt
OutputDir={#SourcePath}
OutputBaseFilename={#MyAppName}-v{#MyAppVersion}
Compression=lzma
SolidCompression=yes
ChangesEnvironment=true
ArchitecturesInstallIn64BitMode=x64

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "{#SourcePath}\Set-ClassicTheme.ps1"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\LICENSE.txt"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\MainWindow.xaml"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\addgui.reg"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\removegui.reg"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\preservegui.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\standard.theme"; DestDir: "{win}\Resources\Ease of Access Themes"; Flags: ignoreversion; Tasks: extrathemes
Source: "{#SourcePath}\classic.theme"; DestDir: "{win}\Resources\Ease of Access Themes"; Flags: ignoreversion; Tasks: extrathemes
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Tasks]
Name: "extrathemes"; Description: "Install Windows Standard and Windows Classic color schemes"

[Run]
Filename: "{sys}\reg.exe"; Parameters: "IMPORT addgui.reg"; WorkingDir: "{app}"; Flags: runascurrentuser waituntilterminated
Filename: "{sys}\schtasks.exe"; Parameters: "/CREATE /TN ""Preserve Classic Theme Settings"" /XML preservegui.xml"; WorkingDir: "{app}"; Flags: runascurrentuser waituntilterminated

[UninstallRun]
Filename: "{sys}\schtasks.exe"; Parameters: "/DELETE /TN ""Preserve Classic Theme Settings"" /F"; WorkingDir: "{app}"; Flags: waituntilterminated
Filename: "{sys}\reg.exe"; Parameters: "IMPORT removegui.reg"; WorkingDir: "{app}"; Flags: waituntilterminated

[Code]
{
The following code, along with environment.iss, was adapted from Wojciech Mleczek's post on Stack Overflow.
For more information, go to https://stackoverflow.com/a/46609047
}

procedure CurStepChanged(CurStep: TSetupStep);
begin
    if CurStep = ssPostInstall 
    then EnvAddPath(ExpandConstant('{app}'));
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
    if CurUninstallStep = usPostUninstall
    then EnvRemovePath(ExpandConstant('{app}'));
end;
